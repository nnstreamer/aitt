plugins {
    id 'com.android.library'
    id "de.undercouch.download" version "5.0.1"
}

def thirdPartyDir = new File ("${rootProject.projectDir}/third_party")

def mosquittoDir = new File("${thirdPartyDir}/mosquitto-2.0.14")

android {
    compileSdkVersion 31
    ndkVersion "21.3.6528147"
    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments '-DCMAKE_VERBOSE_MAKEFILE=1'
                arguments '-DCMAKE_INSTALL_PREFIX:PATH=/usr'
                arguments '-DANDROID_STL=c++_shared'
                arguments "-DANDROID_NDK_HOME=${System.env.ANDROID_NDK_ROOT}"
                arguments '-DWITH_STATIC_LIBRARIES=ON'
                arguments '-DWITH_TLS=OFF'
                arguments '-DWITH_TLS_PSK=OFF'
                arguments '-DWITH_CJSON=OFF'
                arguments '-DWITH_APPS=OFF'
                arguments '-DDOCUMENTATION=OFF'
                cppFlags "-std=c++17"
                abiFilters 'arm64-v8a', 'x86'
                targets "libmosquitto", "mosquittopp", "libmosquitto_static", "mosquittopp_static"
            }
        }
    }
    buildFeatures {
        prefab false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "${mosquittoDir}/CMakeLists.txt"
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task downloadMosquitto(type: Download) {
    doFirst {
        println("Downloading Mosquitto")
    }
    src "https://github.com/eclipse/mosquitto/archive/refs/tags/v2.0.14.zip"
    dest new File(thirdPartyDir, "mosquitto-2.0.14.zip")
    onlyIfModified true
}

task unzipMosquitto(type: Copy, dependsOn: downloadMosquitto) {
    doFirst {
        println("Unzipping Mosquitto")
    }
    from zipTree(downloadMosquitto.dest)
    into thirdPartyDir
    onlyIf { !mosquittoDir.exists() }
}

preBuild.dependsOn(unzipMosquitto)
